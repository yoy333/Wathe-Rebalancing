plugins {
	id 'fabric-loom' version '1.13-SNAPSHOT'
	id "io.github.ladysnake.chenille" version "0.14.0"
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

chenille {
	configurePublishing {
		withLadysnakeMaven()
		withGithubRelease()
		withModrinthRelease()
	}
}

repositories {
	mavenLocal()

    maven {
        name "Modrinth"
        url "https://api.modrinth.com/maven"
        content {
            includeGroup "maven.modrinth"
        }
    }

	maven {
        url "https://maven.terraformersmc.com/releases/"
        name "Mod Menu"
    }

	// CCA, Ratatouille
	maven {
		name "Ladysnake Mods"
		url "https://maven.ladysnake.org/releases"
	}

	maven {
        url "https://maven.uuid.gg/releases"
        name "Datasync"
    }

	maven {
        url "https://maven.maxhenkel.de/repository/public"
    }
}

loom {
	accessWidenerPath = file("src/main/resources/trainmurdermystery.accesswidener")
}

fabricApi {
	configureDataGeneration() {
		modId = "trainmurdermystery"
		client = true
	}
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// Libraries
	// Ratatouille
	modIncludeImplementation "dev.upcraft.datasync:datasync-minecraft-${project.minecraft_version}-fabric:${project.datasync_version}"
	modImplementation "dev.doctor4t:ratatouille:${project.ratatouille_version}"
	// CCA
	modImplementation include ("org.ladysnake.cardinal-components-api:cardinal-components-base:${project.cca_version}")
	modImplementation include ("org.ladysnake.cardinal-components-api:cardinal-components-world:${project.cca_version}")
	modImplementation include ("org.ladysnake.cardinal-components-api:cardinal-components-entity:${project.cca_version}")
	modImplementation include ("org.ladysnake.cardinal-components-api:cardinal-components-scoreboard:${project.cca_version}")
	// Midnightlib
	modIncludeImplementation "maven.modrinth:midnightlib:${project.midnightlib_version}"

	// Runtime mods
	modLocalImplementation "com.terraformersmc:modmenu:${mod_menu_version}"

	modImplementation "de.maxhenkel.voicechat:voicechat-api:${voicechat_api_version}"
	modRuntimeOnly "maven.modrinth:simple-voice-chat:fabric-${voicechat_version}"

	modImplementation "maven.modrinth:sodium:${sodium_version}-fabric"
	modImplementation "maven.modrinth:iris:${iris_version}-fabric"
	modRuntimeOnly("org.anarres:jcpp:${jcpp_version}")
	modImplementation("io.github.douira:glsl-transformer:${glslTransformer_version}")
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}